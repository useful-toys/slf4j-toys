plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'jacoco'
}

group = 'org.usefultoys'
version = '1.8.0'

compileJava.options.encoding = 'UTF-8'
compileJava.options.debug = false

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.28'
    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
    testCompile 'junit:junit:4.12'
    compileOnly 'ch.qos.logback:logback-core:1.2.3'
    compileOnly 'ch.qos.logback:logback-classic:1.2.3'
    compile files(org.gradle.internal.jvm.Jvm.current().toolsJar)
    compileOnly 'com.google.auto.service:auto-service:1.0-rc6'
    annotationProcessor 'com.google.auto.service:auto-service:1.0-rc6'
    compileOnly 'org.jetbrains:annotations:17.0.0'
}

javadoc {
    options.charSet = 'UTF-8'
    options.author = false
    options.noDeprecatedList = true
    options.noHelp = true
}

/**** Upload para Maven Central ****/
/* http://central.sonatype.org/pages/gradle.html */

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'slf4j-toys'
                packaging 'jar'
                description 'A collection of useful practices that complement SLF4J and promotes clean and organized log files.'
                url 'https://github.com/useful-toys/slf4j-toys'
                inceptionYear '2010'

                scm {
                    connection 'scm:git:git@github.com/useful-toys/slf4j-toys.git'
                    developerConnection 'scm:git:git@github.com/useful-toys/slf4j-toys.git'
                    url 'git@github.com/useful-toys/slf4j-toys.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name 'Daniel Felix Ferber'
                        email 'dff4321@gmail.com'
                        organization 'None'
                        organizationUrl 'https://github.com/danielferber'
                    }
                }
            }
        }
    }
}